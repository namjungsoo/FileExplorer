apply plugin: 'com.android.application'

android {
    compileSdkVersion 27

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        applicationId 'com.duongame'
        buildConfigField "boolean", "SHOW_AD", "true"
        buildConfigField "boolean", "PREVIEW_ZIP", "false"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName "1.0.0"
    }

    signingConfigs {
        release {
            storeFile file("../fileexplorer.jks")
            storePassword "love77"
            keyAlias "comicz"
            keyPassword "love77"
        }
    }

    sourceSets {
        main {
            // eclipse의 폴더명과 동일하게 사용함
            jniLibs.srcDirs = ["src/main/libs"]

            // android studio가 직접 컴파일하지 않게 함
            jni.srcDirs = []
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        jumboMode true
        javaMaxHeapSize "4g"
    }

    flavorDimensions("project", "edition")

    productFlavors {
        main {
            dimension "edition"
        }

        // 1. 코믹제트
        comicz {
            //admob
            buildConfigField "String", "INTERSTITIAL_ID", "\"ca-app-pub-5576037828251153/9737551820\""
            buildConfigField "String", "POPUP_ID",        "\"ca-app-pub-5576037828251153/2214285028\""
            buildConfigField "String", "BANNER_ID",       "\"ca-app-pub-5576037828251153/8260818625\""
            //ga
            buildConfigField "String", "GA_TRACKING_ID", "\"UA-61499807-13\""
            buildConfigField "boolean", "PREVIEW_ZIP", "true"
            applicationIdSuffix '.comicz'
            dimension "project"
            versionCode 34
            versionName "1.3.2"
            addManifestPlaceholders([APP_NAME: "@string/comicz_name_free"])
        }

        // 2. 파일탐색기
        file {
            //admob
            buildConfigField "String", "INTERSTITIAL_ID", "\"ca-app-pub-5576037828251153/6911470470\""
            buildConfigField "String", "POPUP_ID",        "\"ca-app-pub-5576037828251153/3680115556\""
            buildConfigField "String", "BANNER_ID",       "\"ca-app-pub-5576037828251153/9457742767\""
            //ga
            buildConfigField "String", "GA_TRACKING_ID", "\"UA-61499807-14\""
            buildConfigField "boolean", "PREVIEW_ZIP", "false"
            applicationIdSuffix '.file'
            dimension "project"
            versionCode 14
            versionName "1.1.2"
            addManifestPlaceholders([APP_NAME: "@string/file_name_free"])
        }

        // 유료(현재 사용하지 않음)
        pro {
            buildConfigField "boolean", "SHOW_AD", "false"
            dimension "edition"
        }
        // 무료
        free {
            applicationIdSuffix '.free'
            dimension "edition"
        }
    }

    // Android Studio 3.1에서 주석처리함
    variantFilter { variant ->
        // 메인일 경우에 현재 사용하지 않으므로 무시하라
        if (variant.getFlavors().get(1).name.equals('main')) {
            variant.setIgnore(true)
        }
        // 프로일 경우에 현재 사용하지 않으므로 무시하라
        if (variant.getFlavors().get(1).name.equals('pro')) {
            variant.setIgnore(true)
        }
    }

    // Android studio 3.1.0 이상
    // D:\git\FileExplorer\explorer\release에 output을 내도록 수정
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "../../release/" + outputFileName
        }
    }

    //gnustl 사용안함
//    packagingOptions {
//        pickFirst "lib/armeabi-v7a/libgnustl_shared.so"
//        pickFirst "lib/armeabi/libgnustl_shared.so"
//    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //implementation files('libs/sevenzip4j.jar')
    //android
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    //zip
    implementation 'net.lingala.zip4j:zip4j:1.2.4'
    //encoding
    implementation 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'
    //GIF
    implementation 'com.felipecsl:gifimageview:2.1.0'
    implementation 'com.github.chrisbanes:PhotoView:2.0.0'
    //GLIDE
    implementation 'com.github.bumptech.glide:glide:4.1.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.1.1'
    //firebase - 11.6.0에서 변경하지 말것
    implementation 'com.google.firebase:firebase-crash:12.0.1'
    implementation 'com.google.firebase:firebase-core:12.0.1'
    implementation 'com.google.android.gms:play-services-analytics:12.0.1'
    implementation 'com.google.android.gms:play-services-ads:12.0.1'
    implementation 'com.google.firebase:firebase-config:12.0.1'
    implementation 'joda-time:joda-time:2.9.9'
    //FileUtils
    implementation 'commons-io:commons-io:2.6'
    implementation 'org.apache.commons:commons-compress:1.15'
    implementation project(':lib7z')
    implementation project(':unrar-android')
    implementation 'com.github.GrenderG:Toasty:1.2.8'
    implementation('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true
    }
}

apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

// ADD THIS AT THE BOTTOM
apply plugin: 'com.google.gms.google-services'

