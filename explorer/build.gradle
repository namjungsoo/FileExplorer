apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId 'com.duongame'
        buildConfigField "boolean", "SHOW_AD", "true"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 2
        versionName "1.0.1"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions("project", "edition")

    productFlavors {
        main {
            dimension "edition"
        }

        // 파일탐색기
        file {
            buildConfigField "String", "BANNER_ID", "\"ca-app-pub-5576037828251153/8260818625\""
            buildConfigField "String", "POPUP_ID", "\"ca-app-pub-5576037828251153/2214285028\""
            buildConfigField "String", "INTERSTITIAL_ID", "\"ca-app-pub-5576037828251153/9737551820\""
            applicationIdSuffix '.file'
            dimension "project"
            versionCode 1
            versionName "1.0.0"
            addManifestPlaceholders([APP_NAME: "@string/comicz_name_free"])
        }
        // 코믹제트
        comicz {
            buildConfigField "String", "BANNER_ID", "\"ca-app-pub-5576037828251153/8260818625\""
            buildConfigField "String", "POPUP_ID", "\"ca-app-pub-5576037828251153/2214285028\""
            buildConfigField "String", "INTERSTITIAL_ID", "\"ca-app-pub-5576037828251153/9737551820\""
            applicationIdSuffix '.comicz'
            dimension "project"
            versionCode 8
            versionName "1.0.7"
            addManifestPlaceholders([APP_NAME: "@string/comicz_name_free"])
        }

        // 유료
        pro {
            buildConfigField "boolean", "SHOW_AD", "false"
            dimension "edition"
        }
        // 무료
        free {
            applicationIdSuffix '.free'
            dimension "edition"
        }

    }
    variantFilter { variant ->
        // 메인일 경우에 현재 사용하지 않으므로 무시하라
        if (variant.getFlavors().get(1).name.equals('main')) {
            variant.setIgnore(true);
        }
        // 프로일 경우에 현재 사용하지 않으므로 무시하라
        if (variant.getFlavors().get(1).name.equals('pro')) {
            variant.setIgnore(true);
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    //android
    implementation 'com.android.support:appcompat-v7:25.3.1'
    implementation 'com.android.support:design:25.3.1'

    //zip
    implementation 'net.lingala.zip4j:zip4j:1.2.4'

    //encoding
    implementation 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'

    //GIF
    implementation 'com.felipecsl:gifimageview:2.1.0'
    implementation 'com.github.chrisbanes:PhotoView:2.0.0'

    //GLIDE
    implementation 'com.github.bumptech.glide:glide:4.1.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.1.1'

    //firebase
    implementation 'com.google.firebase:firebase-crash:10.0.1'
    implementation 'com.google.firebase:firebase-core:10.0.1'

    implementation 'com.google.android.gms:play-services-analytics:10.0.1'
    implementation 'com.google.android.gms:play-services-ads:10.0.1'

    implementation 'joda-time:joda-time:2.9.9'
}

// ADD THIS AT THE BOTTOM
apply plugin: 'com.google.gms.google-services'
