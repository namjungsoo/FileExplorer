apply plugin: 'com.android.application'

android {
    compileSdkVersion 27

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        multiDexEnabled true
        applicationId 'com.duongame'
        buildConfigField "boolean", "SHOW_AD", "true"
        buildConfigField "boolean", "PREVIEW_ZIP", "false"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.0"
    }

    signingConfigs {
        release {
            storeFile file("../fileexplorer.jks")
            storePassword "love77"
            keyAlias "comicz"
            keyPassword "love77"
        }
    }

    sourceSets {
        main {
            // eclipse의 폴더명과 동일하게 사용함
            jniLibs.srcDirs = ["src/main/libs"]

            // android studio가 직접 컴파일하지 않게 함
            jni.srcDirs = []
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.all {
                    def date = new Date()
                    def formattedDate = date.format('yyyyMMddHHmmss')
                    outputFileName = "${variant.name}-${variant.versionName}-${formattedDate}.apk"
                }
            }
        }
        debug {
            signingConfig signingConfigs.release
        }
    }

    dexOptions {
        jumboMode true
        javaMaxHeapSize "4g"
    }

    flavorDimensions("project", "edition", "mode")

    productFlavors {
        main {
            dimension "edition"
        }

        development {
            dimension "mode"
            minSdkVersion 21
            resConfigs("ko", "xxhdpi")
            aaptOptions.cruncherEnabled = false
        }
        production {
            dimension "mode"
            minSdkVersion 14
        }

        // 1. 코믹제트
        comicz {
            //admob
            buildConfigField "String", "INTERSTITIAL_ID", "\"ca-app-pub-8174809468024854/1821427640\""
            buildConfigField "String", "POPUP_ID", "\"ca-app-pub-8174809468024854/2751365931\""
            buildConfigField "String", "BANNER_ID", "\"ca-app-pub-8174809468024854/8003692618\""
            //ga
            buildConfigField "String", "GA_TRACKING_ID", "\"UA-61499807-13\""
            buildConfigField "boolean", "PREVIEW_ZIP", "true"
            applicationIdSuffix '.comicz'
            dimension "project"
            versionCode 42
            versionName "1.4.0"
            addManifestPlaceholders([APP_NAME: "@string/comicz_name_free"])
        }

        // 2. 파일탐색기
        file {
            //admob
            buildConfigField "String", "INTERSTITIAL_ID", "\"ca-app-pub-8174809468024854/9810925855\""
            buildConfigField "String", "POPUP_ID", "\"ca-app-pub-8174809468024854/4034665627\""
            buildConfigField "String", "BANNER_ID", "\"ca-app-pub-8174809468024854/2530012260\""
            //ga
            buildConfigField "String", "GA_TRACKING_ID", "\"UA-61499807-14\""
            buildConfigField "boolean", "PREVIEW_ZIP", "false"
            applicationIdSuffix '.file'
            dimension "project"
            versionCode 22
            versionName "1.2.0"
            addManifestPlaceholders([APP_NAME: "@string/file_name_pro"])
        }

        // 유료(현재 사용하지 않음)
        pro {
            buildConfigField "boolean", "SHOW_AD", "false"
            applicationIdSuffix '.pro'
            dimension "edition"
        }
        // 무료
        free {
            buildConfigField "boolean", "SHOW_AD", "true"
            applicationIdSuffix '.free'
            dimension "edition"
        }
    }

    // Android Studio 3.1에서 주석처리함
    variantFilter { variant ->
        // 메인일 경우에 현재 사용하지 않으므로 무시하라
        if (variant.getFlavors().get(1).name.equals('main')) {
            variant.setIgnore(true)
        }
        // 프로일 경우에 현재 사용하지 않으므로 무시하라
//        if (variant.getFlavors().get(1).name.equals('pro')) {
//            variant.setIgnore(true)
//        }
    }

    // Android studio 3.1.0 이상
    // D:\git\FileExplorer\explorer\release에 output을 내도록 수정
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "../../release/" + outputFileName
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(path: ':lib7z')
    implementation project(path: ':unrar-android')
    //android
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    //zip
    implementation 'net.lingala.zip4j:zip4j:1.2.4'
    //encoding
    implementation 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'
    //GIF
    implementation 'com.felipecsl:gifimageview:2.1.0'
    implementation 'com.github.chrisbanes:PhotoView:2.0.0'
    //GLIDE
    implementation 'com.github.bumptech.glide:glide:4.7.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'
    //firebase
    implementation 'com.google.firebase:firebase-crash:16.2.0'
    implementation 'com.google.firebase:firebase-core:16.0.3'
    implementation 'com.google.android.gms:play-services-analytics:16.0.3'
    implementation 'com.google.android.gms:play-services-ads:15.0.1'
    implementation 'com.google.firebase:firebase-config:16.0.0'
    implementation 'joda-time:joda-time:2.9.9'
    //FileUtils
    implementation 'commons-io:commons-io:2.6'
    implementation 'org.apache.commons:commons-compress:1.15'
    implementation 'com.github.GrenderG:Toasty:1.2.8'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.4@aar') {
        transitive = true
    }
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    //dropbox
    implementation 'com.dropbox.core:dropbox-core-sdk:3.0.7'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.android.support:multidex:1.0.3'

    // Google Play services Auth split
    implementation 'com.google.android.gms:play-services-auth:16.0.0'
    // Google Api Client library and Android extension
    implementation 'com.google.api-client:google-api-client:1.23.0'
    implementation 'com.google.api-client:google-api-client-android:1.22.0'
    // Change to the API you want to access:
    // Below is just an example for People REST API access
    implementation 'com.google.apis:google-api-services-people:v1-rev4-1.22.0'
    implementation 'com.google.android.gms:play-services-drive:15.0.1'
    //implementation 'com.google.android.gms:play-services-auth:11.6.0'
    implementation 'com.google.api-client:google-api-client:1.23.0'
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.23.0'
    implementation 'com.google.apis:google-api-services-drive:v3-rev110-1.23.0'
    //GsonFactory
    implementation 'com.google.http-client:google-http-client-gson:1.19.0'
    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'

}

apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}

// ADD THIS AT THE BOTTOM
apply plugin: 'com.google.gms.google-services'
